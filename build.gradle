plugins {
	id 'scala'
	id 'application'
	id "com.github.johnrengelman.shadow" version "2.0.2"
}

group = 'com.amdelamar'
version = '0.1.0'
jar.baseName = 'vertx-scala'
jar.enabled = false
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Scala Lang
    compile 'org.scala-lang:scala-library:2.12.+'
    
    // Vertx
    compile 'io.vertx:vertx-lang-scala_2.12:3.5.+'
    compile 'io.vertx:vertx-core:3.5.+'
    compile 'io.vertx:vertx-web:3.5.+'

    // test dependencies
    testCompile 'junit:junit:4.12'
}

mainClassName = 'hello.Server'
def mainVerticleName = 'hello.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'
if (System.getProperty("os.name").toLowerCase().contains("windows")) {
    doOnChange = '.\\gradlew classes'
}

// Vertx redeploy
run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

shadowJar {
    classifier = null
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
}
